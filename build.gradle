buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.8'
        classpath 'org.asciidoctor:asciidoctorj:1.5.2'
        classpath 'org.ajoberstar:gradle-git:1.2.0'
        classpath 'com.bluepapa32:gradle-watch-plugin:0.1.5'
        classpath 'org.kordamp.gradle:livereload-gradle-plugin:0.2.1'
    }
}
description = 'Swagger2Markup documentation'
version = '0.7.0'
group = 'io.github.robwin'

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'com.bluepapa32.watch'
apply plugin: 'org.kordamp.gradle.livereload'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

asciidoctor {
    sources {
        include 'index.adoc'
    }
    backends = ['html5']
}

githubPages {
    repoUri = 'https://github.com/Swagger2Markup/swagger2markup-docs.git'

    credentials {
        username = project.githubUser
        password = project.githubPassword
    }

    pages {
        from file(asciidoctor.outputDir.path + '/html5')
    }
}

watch {
    asciidoc {
        files fileTree('src/docs/asciidoc')
        tasks 'asciidoctor'
    }
}

liveReload {
    docRoot asciidoctor.outputDir.canonicalPath
}

publishGhPages.dependsOn asciidoctor

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
